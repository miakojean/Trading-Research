//+------------------------------------------------------------------+
//|                                               CaladriusTears.mq5 |
//|                                  Copyright 2025, MetaQuotes Ltd. |
//|                                             https://www.mql5.com |
//+------------------------------------------------------------------+
#property copyright "Copyright 2025, MetaQuotes Ltd."
#property link      "https://www.mql5.com"
#property version   "1.00"

//+------------------------------------------------------------------+
//| Input parameters                                                 |
//+------------------------------------------------------------------+
input double LotSize        = 0.5;      // Taille du lot
input int    MagicNumber    = 12345;  // Magic Number
input int    Slippage       = 3;      // Slippage en points
input double StopLoss       = 30000;  // Stop Loss en points
input double TakeProfit     = 50000;  // Take Profit en points
input double MaxSpread      = 450;    // Ecart max autoris√© en points

// Handles et buffers pour Heiken Ashi
int ha_handle;
double ha_open_buffer[], ha_close_buffer[], ha_high_buffer[], ha_low_buffer[];

// Handles et buffers pour Ichimoku
int ichimokuHandle;

// Variables pour RSI
int rsiHandle;

#include <Trade\Trade.mqh>
#include <Trade\PositionInfo.mqh>
CTrade trade;
CPositionInfo position;

//+------------------------------------------------------------------+
//| Expert initialization function                                   |
//+------------------------------------------------------------------+
int OnInit()
{
    // V√©rification des param√®tres d'entr√©e
    if(LotSize <= 0 || MagicNumber <= 0 || Slippage < 0 || StopLoss <= 0 || TakeProfit <= 0 || MaxSpread <= 0)
    {
        Print("Param√®tres d'entr√©e invalides!");
        return(INIT_PARAMETERS_INCORRECT);
    }

    // Initialisation Heiken Ashi
    ha_handle = iCustom(_Symbol, _Period, "Examples\\Heiken_Ashi");
    if(ha_handle == INVALID_HANDLE)
    {
        Print("√âchec de l'initialisation de Heiken Ashi!");
        return(INIT_FAILED);
    }

    // Initialisation du RSI
    rsiHandle = iRSI(_Symbol, _Period, 14, PRICE_CLOSE);
    if(rsiHandle == INVALID_HANDLE)
    {
        Print("√âchec de l'initialisation du RSI!");
        return(INIT_FAILED);
    }
    
    trade.SetExpertMagicNumber(MagicNumber);
    trade.SetDeviationInPoints(Slippage);
    
    // Configuration des s√©ries temporelles
    ArraySetAsSeries(ha_open_buffer, true);
    ArraySetAsSeries(ha_close_buffer, true);
    ArraySetAsSeries(ha_high_buffer, true);
    ArraySetAsSeries(ha_low_buffer, true);
    
    // Cr√©ation de l'indicateur Ichimoku
    ichimokuHandle = iIchimoku(_Symbol, PERIOD_CURRENT, 9, 26, 52);

    // Ajout de l'indicateur Ichimoku au graphique
    if(!ChartIndicatorAdd(0, 0, ichimokuHandle))
      {
          Print("Erreur : Impossible d'ajouter Ichimoku sur le graphique.");
      }

    Print("Expert Advisor initialis√© avec succ√®s!");
    return(INIT_SUCCEEDED);
}

//+------------------------------------------------------------------+
//| Expert deinitialization function                                 |
//+------------------------------------------------------------------+
void OnDeinit(const int reason)
{
    if(ha_handle != INVALID_HANDLE)
    {
        IndicatorRelease(ha_handle);
    }
}

//+------------------------------------------------------------------+
//| Expert tick function                                             |
//+------------------------------------------------------------------+
void OnTick()
{
   static datetime last_bar_time = 0;
   datetime current_bar_time = iTime(_Symbol, _Period, 0);

   if(last_bar_time == current_bar_time) 
      return;
      
   last_bar_time = current_bar_time;

   if(IsFridayEvening())
   {
      Print("üïõ Vendredi soir : Fermeture de toutes les positions !");
      CloseAllPositions();
   }

   SystemSignals();

   double capital = AccountInfoDouble(ACCOUNT_BALANCE);
   Print("Solde actuel du compte : ", capital);
}

//+------------------------------------------------------------------+
//| Configuration pour le RSI                                        |
//+------------------------------------------------------------------+
double GetRSI()
{
   double rsiBuffer[];

   if(rsiHandle > 0)
   {
      if(CopyBuffer(rsiHandle, 0, 0, 1, rsiBuffer) > 0)
      {
         Print("Valeur actuelle du RSI : ", rsiBuffer[0]); // Affichage du RSI
         return rsiBuffer[0]; // Retourne la valeur actuelle
      }
   }

   Print("‚ùå Erreur : Impossible de r√©cup√©rer le RSI.");
   return 0; // Retourne 0 si erreur
}

string GetRSISignal()
{
   double rsi = GetRSI();

   if(rsi > 70)
   {
      Print("üöÄ RSI √©lev√© ‚Üí Signal de VENTE !");  
      return "SELL";
   }
   else if(rsi < 30)
   {
      Print("üìà RSI bas ‚Üí Signal d'ACHAT !");
      return "BUY";
   }

   Print("‚öñÔ∏è RSI neutre ‚Üí Pas de signal.");
   return "NEUTRAL";
}

//+------------------------------------------------------------------+
//| Ichimoku Kynko Hyo                                               |
//+------------------------------------------------------------------+
string GetSignal()
{
   double kijunSen[], price;

   if(ichimokuHandle > 0) // V√©rification que l'indicateur est bien charg√©
   {
      if(CopyBuffer(ichimokuHandle, 1, 0, 1, kijunSen) > 0)
      {
         price = SymbolInfoDouble(_Symbol, SYMBOL_BID); // Prix actuel
         
         // V√©rification des conditions de signal
         if(price > kijunSen[0])
         {
            Print("üîπ Le prix est **au-dessus** de la Kijun-Sen ‚Üí Signal ACHAT !");
            return "BUY";
         }
         else if(price < kijunSen[0])
         {
            Print("üîª Le prix est **en dessous** de la Kijun-Sen ‚Üí Signal VENTE !");
            return "SELL";
         }
         else
         {
            Print("‚öñÔ∏è Le prix est **exactement** sur la Kijun-Sen ‚Üí Pas de signal.");
            return "NEUTRAL";
         }

      }
   }

   Print("‚ùå Erreur : Impossible de r√©cup√©rer la Kijun-Sen.");
   return "ERROR"; // Retourne "ERROR" en cas de probl√®me
}

string GetKumoPosition()
{
   double senkouSpanA[], senkouSpanB[], price;
   int shift = 26; // Le d√©calage standard du nuage Ichimoku

   if(ichimokuHandle > 0)
   {
      if(CopyBuffer(ichimokuHandle, 2, shift, 1, senkouSpanA) > 0 &&
         CopyBuffer(ichimokuHandle, 3, shift, 1, senkouSpanB) > 0)
      {
         price = SymbolInfoDouble(_Symbol, SYMBOL_BID);

         double upper = MathMax(senkouSpanA[0], senkouSpanB[0]);
         double lower = MathMin(senkouSpanA[0], senkouSpanB[0]);

         if(price > upper)
         {
            Print("‚òÅÔ∏è Le prix est **au-dessus** du nuage ‚Üí Biais haussier !");
            return "ABOVE_CLOUD";
         }
         else if(price < lower)
         {
            Print("‚òÅÔ∏è Le prix est **en dessous** du nuage ‚Üí Biais baissier !");
            return "BELOW_CLOUD";
         }
         else
         {
            Print("üå´Ô∏è Le prix est **dans** le nuage ‚Üí Zone d'incertitude.");
            return "INSIDE_CLOUD";
         }
      }
   }

   Print("‚ùå Erreur : Impossible de r√©cup√©rer les lignes du nuage.");
   return "ERROR";
}


//+------------------------------------------------------------------+
//| Les diff√©rents signaux du syst√®me de tradding                    |
//+------------------------------------------------------------------+
void SystemSignals()
{
    // R√©cup√©ration des donn√©es Heiken Ashi
    if(CopyBuffer(ha_handle, 0, 0, 3, ha_open_buffer) < 3 ||
       CopyBuffer(ha_handle, 1, 0, 3, ha_high_buffer) < 3 ||
       CopyBuffer(ha_handle, 2, 0, 3, ha_low_buffer) < 3 ||
       CopyBuffer(ha_handle, 3, 0, 3, ha_close_buffer) < 3) 
    {
      Print("‚ö†Ô∏è √âchec de la copie des valeurs Heiken Ashi !");
      return;
    }

    if(ArraySize(ha_open_buffer) < 3) 
    {
      Print("‚ö†Ô∏è Erreur : Pas assez de donn√©es dans les buffers !");
      return;
    }

    // V√©rification de l'√©cart
    double spread = SymbolInfoDouble(_Symbol, SYMBOL_ASK) - SymbolInfoDouble(_Symbol, SYMBOL_BID);
    if(spread > MaxSpread)
    {
        Print("‚ö†Ô∏è √âcart trop √©lev√© : ", spread, " points. Position non ouverte.");
        return;
    }

    // V√©rification des positions existantes
    bool hasLongPosition = false;
    bool hasShortPosition = false;

    // Signaux par Indicateurs
    string signal = GetSignal();
    string rsiSignal = GetRSISignal();

    // Asiatic Session Check
    bool isAsianSession = IsAsianSession();
    
    if(PositionSelect(_Symbol))
    {
      if(PositionGetInteger(POSITION_TYPE) == POSITION_TYPE_BUY)
         hasLongPosition = true;
      else if(PositionGetInteger(POSITION_TYPE) == POSITION_TYPE_SELL)
         hasShortPosition = true;
    }

    // Signal d'achat - Deux bougies haussi√®res cons√©cutives
    if( ha_close_buffer[0] > ha_open_buffer[0] && 
        ha_close_buffer[1] > ha_open_buffer[1] && 
        ha_close_buffer[2] > ha_open_buffer[2] && 
        signal == "BUY" && rsiSignal == "NEUTRAL"
        && isAsianSession == false)
    {
        Print("üîπ Signal ACHAT - Deux bougies bleues cons√©cutives");
        
        // Si nous avons d√©j√† une position courte, on la ferme
        if(hasShortPosition)
        {
            CloseAllPositions();
            Print("Position courte ferm√©e pour ouvrir un achat");
        }
        
        // Si pas de position longue ouverte, on ouvre une position
        if(!hasLongPosition)
        {
            OpenBuyPosition();
        }
    }
    // Signal de vente - Deux bougies baissi√®res cons√©cutives
    else if(ha_close_buffer[0] < ha_open_buffer[0] && 
        ha_close_buffer[1] < ha_open_buffer[1] && 
        ha_close_buffer[2] < ha_open_buffer[2] &&
        signal == "SELL" && rsiSignal == "NEUTRAL" && isAsianSession == false)
    {
        Print("üîª Signal VENTE - Deux bougies rouges cons√©cutives");
        
        // Si nous avons d√©j√† une position longue, on la ferme
        if(hasLongPosition)
        {
            CloseAllPositions();
            Print("Position longue ferm√©e pour ouvrir une vente");
        }
        
        // Si pas de position courte ouverte, on ouvre une position
        if(!hasShortPosition)
        {
            OpenSellPosition();
        }
    }
}

//+------------------------------------------------------------------+
//| Ferme toutes les positions                                       |
//+------------------------------------------------------------------+
void CloseAllPositions()
{
    for(int i = PositionsTotal()-1; i >= 0; i--)
    {
        if(position.SelectByIndex(i))
        {
            if(position.Symbol() == _Symbol && position.Magic() == MagicNumber)
            {
                trade.PositionClose(position.Ticket());
                Print("Position ferm√©e : Ticket #", position.Ticket());
            }
        }
    }
}

//+------------------------------------------------------------------+
//| Ouvrir une position d'achat                                      |
//+------------------------------------------------------------------+
void OpenBuyPosition()
{
    double ask = SymbolInfoDouble(_Symbol, SYMBOL_ASK);
    double point = SymbolInfoDouble(_Symbol, SYMBOL_POINT);
    
    double stop_loss_price = ask - StopLoss * point;
    double take_profit_price = ask + TakeProfit * point;

    double dynamicLotSize = AdjustLotSize();
    
    // V√©rifier le solde disponible
    double freeMargin = AccountInfoDouble(ACCOUNT_MARGIN_FREE);
    if(freeMargin < 0)
    {
        Print("Erreur : Marge insuffisante pour ouvrir une position d'achat.");
        return;
    }

    if(!trade.Buy(dynamicLotSize, _Symbol, ask, stop_loss_price, take_profit_price, "Achat Heiken Ashi"))
    {
        Print("Erreur lors de l'ouverture de la position d'achat : ", GetLastError());
    }
    else
    {
        Print("Position d'achat ouverte avec succ√®s ! Prix: ", ask, " SL: ", stop_loss_price, " TP: ", take_profit_price);
    }
}

//+------------------------------------------------------------------+
//| Ouvrir une position de vente                                     |
//+------------------------------------------------------------------+
void OpenSellPosition()
{
    double bid = SymbolInfoDouble(_Symbol, SYMBOL_BID);
    double point = SymbolInfoDouble(_Symbol, SYMBOL_POINT);
    
    double stop_loss_price = bid + StopLoss * point;
    double take_profit_price = bid - TakeProfit * point;

    double dynamicLotSize = AdjustLotSize();
    
    // V√©rifier le solde disponible
    double freeMargin = AccountInfoDouble(ACCOUNT_MARGIN_FREE);
    if(freeMargin < 0)
    {
        Print("Erreur : Marge insuffisante pour ouvrir une position de vente.");
        return;
    }

    if(!trade.Sell(dynamicLotSize, _Symbol, bid, stop_loss_price, take_profit_price, "Vente Heiken Ashi"))
    {
        Print("Erreur lors de l'ouverture de la position de vente : ", GetLastError());
    }
    else
    {
        Print("Position de vente ouverte avec succ√®s ! Prix: ", bid, " SL: ", stop_loss_price, " TP: ", take_profit_price);
    }
}

/* La gestion du risque; partie la plus importante de notre syst√®me */

//+------------------------------------------------------------------+
//| Gestion dynamique de la taille de position                       |
//+------------------------------------------------------------------+
double AdjustLotSize()
{
    // Taille du lot par d√©faut, utilis√©e si aucune condition d'ajustement sp√©cifique n'est remplie
    double calculatedLotSize = LotSize; 
    
    // Obtenir la marge libre disponible sur le compte
    double freeMargin = AccountInfoDouble(ACCOUNT_MARGIN_FREE);

    // R√©cup√©rer les signaux Ichimoku et la position par rapport au nuage
    string signal = GetSignal();
    string kumoPosition = GetKumoPosition();
    
    // D√©terminer le type d'ordre et le prix pour le calcul de marge
    // Cette partie doit √™tre robuste m√™me si les signaux ne sont pas BUY/SELL
    ENUM_ORDER_TYPE orderType;
    double price;
    
    if (signal == "BUY")
    {
        orderType = ORDER_TYPE_BUY;
        price = SymbolInfoDouble(_Symbol, SYMBOL_ASK);
    }
    else if (signal == "SELL")
    {
        orderType = ORDER_TYPE_SELL;
        price = SymbolInfoDouble(_Symbol, SYMBOL_BID);
    }
    else
    {
        // Si le signal est neutre ou erreur, on ne peut pas calculer la marge pour une nouvelle transaction,
        // donc on s'en tient au lot par d√©faut.
        Print("‚ÑπÔ∏è Signal Ichimoku neutre ou erreur, pas de calcul de lot dynamique avanc√©. Utilisation du LotSize par d√©faut.");
        return calculatedLotSize; 
    }

    double marginRequiredForOneLot;
    // Calculer la marge requise pour 1.0 lot. V√©rifier si le calcul r√©ussit.
    if (!OrderCalcMargin(orderType, _Symbol, 1.0, price, marginRequiredForOneLot))
    {
        Print("‚ùå Erreur lors du calcul de la marge requise pour 1.0 lot : ", GetLastError(), ". Utilisation du LotSize par d√©faut.");
        return calculatedLotSize; // Retourne le lot par d√©faut en cas d'√©chec du calcul
    }
    
    // --- Logique de gestion du risque dynamique ---

    // Option 1: Doublement du lot si les conditions du nuage Ichimoku sont tr√®s favorables
    // Ceci peut √™tre appliqu√© √† la premi√®re position si l'on veut √™tre plus agressif d√®s le d√©but.
    bool canDoubleLot = false;
    if ((orderType == ORDER_TYPE_BUY && kumoPosition == "ABOVE_CLOUD") || 
        (orderType == ORDER_TYPE_SELL && kumoPosition == "BELOW_CLOUD"))
    {
        canDoubleLot = true;
    }
    
    if (canDoubleLot)
    {
        // On vise √† utiliser un certain pourcentage de la marge libre pour le lot doubl√©
        // Par exemple, 5% du solde du compte ou un pourcentage de la marge libre.
        // Ici, nous allons calculer un lot maximum bas√© sur un pourcentage de la marge libre disponible.
        // C'est plus s√ªr que de simplement doubler le lot sans v√©rification de la marge.
        
        // Pourcentage de marge libre que nous sommes pr√™ts √† risquer pour cette position (ex: 10%)
        // Ajustez cette valeur selon votre tol√©rance au risque.
        double percentageOfFreeMarginToUse = 0.10; 
        
        // Lot maximal que l'on peut ouvrir avec ce pourcentage de marge libre
        double maxAllowedLotBasedOnMargin = (freeMargin * percentageOfFreeMarginToUse) / marginRequiredForOneLot;
        
        // Limiter le lot minimum, maximum et les pas du symbole
        double minLot = SymbolInfoDouble(_Symbol, SYMBOL_VOLUME_MIN);
        double maxLot = SymbolInfoDouble(_Symbol, SYMBOL_VOLUME_MAX);
        double stepLot = SymbolInfoDouble(_Symbol, SYMBOL_VOLUME_STEP);

        // Calculer le lot doubl√© potentiel (si une position existe d√©j√† et qu'on voulait doubler)
        // Sinon, c'est simplement LotSize * 2.0 pour la premi√®re position si canDoubleLot est vrai
        double proposedLot = LotSize * 2.0; // Point de d√©part pour le lot "doubl√©"

        // V√©rifier si une position existe d√©j√† et ajuster le 'proposedLot' en cons√©quence
        if (PositionSelect(_Symbol))
        {
             double currentLot = PositionGetDouble(POSITION_VOLUME);
             // Si on veut vraiment doubler une position existante
             proposedLot = currentLot * 2.0; 
        }

        // Le lot final sera le minimum entre le lot propos√© (doubl√© ou LotSize*2),
        // le lot maximal autoris√© par la marge et le lot maximum du symbole.
        calculatedLotSize = MathMin(proposedLot, maxAllowedLotBasedOnMargin);
        calculatedLotSize = MathMin(calculatedLotSize, maxLot); // S'assurer de ne pas d√©passer le lot max du symbole
        calculatedLotSize = MathMax(calculatedLotSize, minLot); // S'assurer de ne pas √™tre en dessous du lot min

        // Normaliser le lot √† l'√©tape de volume du symbole
        calculatedLotSize = NormalizeDouble(calculatedLotSize / stepLot, 0) * stepLot;

        Print("üöÄ Lot dynamique calcul√© : ", calculatedLotSize, " (bas√© sur le signal et la marge).");
    }
    else
    {
        // Si les conditions de doublement ne sont pas remplies, ou si c'est la premi√®re position sans conditions sp√©ciales,
        // on utilise le LotSize par d√©faut.
        calculatedLotSize = LotSize;

        // Assurez-vous que le LotSize par d√©faut est √©galement valide
        double minLot = SymbolInfoDouble(_Symbol, SYMBOL_VOLUME_MIN);
        double maxLot = SymbolInfoDouble(_Symbol, SYMBOL_VOLUME_MAX);
        double stepLot = SymbolInfoDouble(_Symbol, SYMBOL_VOLUME_STEP);
        
        calculatedLotSize = MathMax(calculatedLotSize, minLot);
        calculatedLotSize = MathMin(calculatedLotSize, maxLot);
        calculatedLotSize = NormalizeDouble(calculatedLotSize / stepLot, 0) * stepLot;

        Print("‚ÑπÔ∏è Utilisation du LotSize par d√©faut : ", calculatedLotSize);
    }

    // V√©rification finale pour s'assurer que le lot n'est pas z√©ro ou n√©gatif.
    if (calculatedLotSize <= 0)
    {
        Print("‚ö†Ô∏è Le calcul du lot a r√©sult√© en une valeur invalide (<= 0). Retourne le LotSize par d√©faut.");
        return LotSize; // Fallback au LotSize d'entr√©e si le calcul a foir√©.
    }

    return calculatedLotSize;
}

/* Eviter les expositions inutiles du capital */

//+------------------------------------------------------------------+
//| Eviter de trader la session asiatique                            |
//+------------------------------------------------------------------+
bool IsAsianSession()
{
    MqlDateTime timeStruct;
    TimeCurrent(timeStruct); // Obtient l'heure actuelle du serveur

    if(timeStruct.hour >= 0 && timeStruct.hour < 7) // Session asiatique (00:00 - 08:00 UTC)
    {   
        return true;
    }

    return false;
}

//+------------------------------------------------------------------+
//| Fermer toutes les positions vendredis                            |
//+------------------------------------------------------------------+
bool IsFridayEvening()
{
    MqlDateTime timeStruct;
    TimeCurrent(timeStruct);

    if(timeStruct.day_of_week == 5 && timeStruct.hour >= 20) // Vendredi apr√®s 22h
    {
        return true;
    }

    return false;
}

//+------------------------------------------------------------------+
//| Eviter le trading de News                                        |
//+------------------------------------------------------------------+